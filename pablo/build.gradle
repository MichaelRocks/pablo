buildscript {
  ext.javaVersion = JavaVersion.VERSION_1_8
  ext.kotlinVersion = '1.5.20'
  ext.shadowVersion = '7.0.0'
  ext.mavenVersion = '3.8.1'

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

apply plugin: 'kotlin'

final boolean isBootstrap = project.path == ':'

// Don't apply the plugin for buildSrc.
if (!isBootstrap) {
  apply plugin: 'io.michaelrocks.pablo'
  pablo {
    artifactName = 'pablo'
    pom {
      name = 'Pablo'
      description = 'A Gradle plugin for repackaging build artifacts and publishing them to a Maven repo.'
      inceptionYear = '2018'
      url = 'https://github.com/michaelrocks/pablo'

      licenses {
        license {
          name = 'The Apache License, Version 2.0'
          url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution = 'repo'
        }
      }
      developers {
        developer {
          id = 'MichaelRocks'
          name = 'Michael Rozumyanskiy'
          email = 'michael.rozumyanskiy@gmail.com'
        }
      }
      scm {
        connection = 'scm:git:git://github.com/michaelrocks/pablo.git'
        developerConnection = 'scm:git:ssh://git@github.com/michaelrocks/pablo.git'
        url = 'https://github.com/michaelrocks/pablo'
      }
    }
  }
} else {
  jar.archiveFileName = 'pablo-bootstrap.jar'
}

targetCompatibility = javaVersion
sourceCompatibility = javaVersion

repositories {
  mavenCentral()
  gradlePluginPortal()
}

dependencies {
  compileOnly gradleApi()

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
  implementation "org.apache.maven:maven-artifact:$mavenVersion"

  final String shadowConfigurationName = isBootstrap ? 'implementation' : 'relocate';
  add(shadowConfigurationName, "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:$shadowVersion") {
    // The shadow plugin contains dependencies in pom.xml, however they're bundled into the fatjar and not needed.
    transitive false
  }
}

compileKotlin {
  kotlinOptions {
    jvmTarget = javaVersion
  }
}